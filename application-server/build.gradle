plugins {
    id "idea"
    id "java"
    alias(libs.plugins.spring.boot4)
    alias(libs.plugins.spring.dependency.management)
    alias(libs.plugins.springdoc.openapi)
    alias(libs.plugins.spotless)
}

group = "com.example"
version = "0.0.1-SNAPSHOT"

java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Modulithを使えないかな
    // https://docs.spring.io/spring-modulith/docs/current-SNAPSHOT/reference/html/
    implementation project(":campaign")

    implementation(libs.spring.boot.starter.web)
    implementation(libs.spring.boot.starter.actuator)
    implementation(libs.spring.boot.starter.opentelemetry)

    implementation(libs.spring.boot.zipkin)
    implementation(libs.spring.boot.docker.compose)

    implementation(libs.micrometer.tracing.bridge)
    implementation(libs.micrometer.prometheus)
    implementation(libs.opentelemetry.exporter.zipkin)

    implementation(libs.springdoc.openapi3)

    developmentOnly(libs.spring.boot.devtools)

    testImplementation(libs.spring.boot.starter.test)
    testImplementation(libs.archunit)
    testImplementation(libs.karate)
    testImplementation(libs.cucumber.reporting)
}

spotless {
    java {
        googleJavaFormat().skipJavadocFormatting()
    }
}

sourceSets {
    test {
        resources {
            srcDir file("src/test/java")
            exclude "**/*.java"
        }
    }
}

tasks.named("test") {
    useJUnitPlatform()
    systemProperty "karate.options", System.properties.getProperty("karate.options")
    systemProperty "karate.env", System.properties.getProperty("karate.env")
    outputs.upToDateWhen { false }
    if (project.hasProperty("excludeTests")) {
        exclude project.property("excludeTests")
    }
}
